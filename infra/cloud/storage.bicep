@description('Globally unique lowercase letters and numbers, 3-24 characters.')
@minLength(3)
@maxLength(24)
param storageAccountName string
param location string = resourceGroup().location
param containerName string = 'imagevault'

resource account 'Microsoft.Storage/storageAccounts@2023-05-01' = {
  name: storageAccountName
  location: location
  kind: 'StorageV2'
  sku: { name: 'Standard_LRS' }
  properties: {
    accessTier: 'Hot'
    supportsHttpsTrafficOnly: true
    minimumTlsVersion: 'TLS1_2'
    allowBlobPublicAccess: false
    // Supports local demo connection strings; VM deployments can disable shared keys.
    allowSharedKeyAccess: true
  }
}
resource blobs 'Microsoft.Storage/storageAccounts/blobServices@2023-05-01' = {
  parent: account
  name: 'default'
  properties: {
    deleteRetentionPolicy: {
      enabled: true
      days: 7
    }
    containerDeleteRetentionPolicy: {
      enabled: true
      days: 7
    }
  }
}
resource container 'Microsoft.Storage/storageAccounts/blobServices/containers@2023-05-01' = {
  parent: blobs
  name: containerName
  properties: { publicAccess: 'None' }
}
output accountUrl string = account.properties.primaryEndpoints.blob
output accountId string = account.id
output container string = container.name
